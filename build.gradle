ext {
    bintrayRepo = 'chrynan'
    libraryGroup = 'com.chrynan'
    libraryOwner = 'chrynan'
    libraryRepo = 'inject'
    libraryVersion = '0.2.0'
    libraryVersionCode = 1
    libraryVersionDescription = 'Minor Release'
    libraryGithubUrl = 'https://github.com/chRyNaN/inject.git'
    libraryLicense = 'Apache-2.0'
}

group libraryGroup
version libraryVersion

buildscript {
    repositories {
        mavenCentral()
        jcenter()
        google()
    }

    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:1.3.61"
        classpath 'com.jfrog.bintray.gradle:gradle-bintray-plugin:1.8.4'
    }
}

apply plugin: 'kotlin-multiplatform'
apply plugin: 'maven-publish'
apply plugin: 'com.jfrog.bintray'

repositories {
    mavenCentral()
    jcenter()
}

kotlin {
    jvm()
    js()

    sourceSets {
        commonMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-common'
            }
        }
        jvmMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib'
                implementation 'javax.inject:javax.inject:1'
            }
        }
        jsMain {
            dependencies {
                implementation 'org.jetbrains.kotlin:kotlin-stdlib-js'
            }
        }
    }
}

configurations {
    compileClasspath
}

publishing {
    publications {
        kotlinMultiplatform {
            artifactId = "inject-common"
        }
        jvm {
            artifactId = "inject-jvm"
        }
        js {
            artifactId = "inject-js"
        }
        metadata {
            artifactId = "inject-metadata"
        }
    }
}

bintray {
    user = project.findProperty("BINTRAY_USER") ?: System.getenv('BINTRAY_USER')
    key = project.findProperty("BINTRAY_KEY") ?: System.getenv('BINTRAY_KEY')
    pkg {
        repo = rootProject.bintrayRepo
        name = rootProject.libraryRepo
        licenses = [rootProject.libraryLicense]
        vcsUrl = rootProject.libraryGithubUrl
        version {
            name = rootProject.libraryVersion
            desc = rootProject.libraryVersionDescription
            released = new Date()
            vcsTag = rootProject.libraryVersion
        }
    }
}

bintrayUpload.doFirst {
    publications = publishing.publications.collect {
        it.name
    }
}

bintrayUpload.dependsOn publishToMavenLocal
